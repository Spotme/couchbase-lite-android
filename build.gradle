apply plugin: 'com.android.library'
apply plugin: 'maven'

buildscript {
    repositories {
	    mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.2'
    }
}

repositories {
    mavenLocal()
    maven { url 'http://files.couchbase.com/maven2/' }
    mavenCentral()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    // workaround for "duplicate files during packaging of APK" issue
    // see https://groups.google.com/d/msg/adt-dev/bl5Rc4Szpzg/wC8cylTWuIEJ
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDir file('jniLibs')
        }
    }
}

def buildAndroidWithArtifacts = System.getProperty("buildAndroidWithArtifacts")

dependencies {

    // this library is only needed by test code (95% certain), and therefore
    // instrumentTestCompile is used rather than compile.
    androidTestCompile 'commons-io:commons-io:2.0.1'

    compile project(':cbl:java-core')
	compile project(':cbl:java-listener')

    compile fileTree(dir: 'libs', include: '*.jar')

    // temporary workaround to enable running tests against Couchbase Lite Listener.
    // once tests are converted over to use mockwebserver, this can be ripped out.
    // see https://github.com/couchbase/couchbase-lite-android/pull/300
    androidTestCompile project(':cbl:java-listener')

    androidTestCompile 'com.squareup.okhttp:mockwebserver:1.2.1'
}